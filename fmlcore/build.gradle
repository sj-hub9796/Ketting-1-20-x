import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java-library'
    id 'maven-publish'
//    id 'org.cadixdev.licenser' //Ketting we use other licensed files and thus cannot use this
    id 'net.minecraftforge.gradleutils'
}

apply from: rootProject.file('build_shared.gradle')

dependencies {
    compileOnly(libs.nulls)

    api(libs.eventbus)

    implementation(project(':fmlloader'))
    implementation(libs.commons.io)
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            'Automatic-Module-Name': 'net.minecraftforge.fmlcore',
            'FMLModType': 'LIBRARY'
        ] as LinkedHashMap)
        attributes([
            'Specification-Title':      'FML',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    '1',
            'Implementation-Title':     'FML',
            'Implementation-Version':   '1.0',
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/fml/')
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:-unchecked'
}

//Ketting start - we use other licensed files and thus cannot use this
/*
license {
    header = rootProject.file('LICENSE-header.txt')
}
*/
//Ketting end
publishing {
    publications.register('mavenJava', MavenPublication).configure {
        from components.java
        artifactId = 'fmlcore'
        pom {
            name = project.name
            description = 'Modifactions to Minecraft to enable mod developers.'
            url = "https://github.com/kettingpowered/Ketting-1-20-x"
            developers {
                developer {
                    id = "justred23"
                    name = "JustRed23"
                }
                developer {
                    id = "c0d3m4513r"
                    name = "C0D3 M4513R"
                }
            }
            scm {
                connection = "scm:git:https://github.com/kettingpowered/Ketting-1-20-x.git"
                //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
                url = "https://github.com/kettingpowered/Ketting-1-20-x"
            }
            PomUtils.setGitHubDetails(pom, 'kettingpowered', 'Ketting-1-20-x')
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/Ketting-1-20-x")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://reposilite.c0d3m4513r.com/Ketting-Server-Releases/"
        }
    }
}
